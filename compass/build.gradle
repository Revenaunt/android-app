buildscript{
    repositories{
        maven{
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies{
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android{
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    /* NOTES on Versioning:
     * Prior to release, increment VERSION_CODE. This should be an integer value.
     * VERSION_NAME should adhere to Semantic Versioning. See http://semver.org/
     *    - MAJOR version when you make incompatible API changes (or significant UI changes)
     *    - MINOR version when you add functionality in a backwards-compatible manner, and
     *    - PATCH version when you make backwards-compatible bug fixes.
     */

    //Version variables, modify these to update versions
    def VERSION_CODE = 42
    def VERSION_NAME = "3.7.3"

    //Configurations that affect all flavors
    defaultConfig{
        //Application id (ie, the package)
        applicationId "org.tndata.android.compass"

        //Min and target SDKs
        minSdkVersion 14
        targetSdkVersion 25

        //Versions
        versionCode VERSION_CODE
        versionName VERSION_NAME

        //GCM
        buildConfigField(
                "String",
                "GCM_SENDER_ID",
                '"' + GcmSenderId + '"' ?: '"Define a GCM sender id"'
        )

        //Other keys
        resValue "string", "google_app_id", GoogleAPIsProjID
        resValue "string", "logging_key", LoggingKey
    }

    //Flavor specific configurations
    buildTypes{
        debug{
            manifestPlaceholders = [fabricApiKey: FabricApiKey, mapsApiKey: MapsDebugApiKey]
            resValue "string", "version_name", VERSION_NAME + ".debug"
            versionNameSuffix ".debug"
        }
        release{
            manifestPlaceholders = [fabricApiKey: FabricApiKey, mapsApiKey: MapsApiKey]
            resValue "string", "version_name", VERSION_NAME

            //Enable proguard for release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding{
        enabled true
    }

    testOptions{
        unitTests.returnDefaultValues = true
    }
}

//Repos where the dependencies should be fetched from
repositories{
    jcenter()
    mavenCentral()
    maven{
        url "https://maven.fabric.io/public"
    }
    maven{
        url "https://jitpack.io"
    }
}

//The actual list of dependencies
dependencies{
    compile project(':model')

    compile('com.doomonafireball.betterpickers:library:1.6.0'){
        exclude group: 'com.android.support', module: 'support-v4'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.0@aar'){
        transitive = true;
    }
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.google.android.gms:play-services-gcm:9.8.0'
    compile 'com.google.android.gms:play-services-location:9.8.0'
    compile 'com.google.android.gms:play-services-maps:9.8.0'
    compile 'com.google.android.gms:play-services-places:9.8.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'me.relex:circleindicator:1.1.5@aar'
    compile 'com.github.clans:fab:1.6.2'
    compile 'es.sandwatch:http-requests:1.0.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.robolectric:robolectric:3.0'

    androidTestCompile 'org.robolectric:robolectric:3.0'
}
